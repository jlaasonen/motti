10 W% = 5
20 H% = 5
30 DIM MAP%(H%,W%)
40 DIM CLAIMED%(2):DIM TAKEN%(2)
50 CLAIMED%(1) = 2
60 TAKEN%(1) = 3
70 CLAIMED%(2) = 4
80 TAKEN%(2) = 5
90 DIM SEARCH%(4,2)
100 SEARCH%(1,1)=1:SEARCH%(1,2)=0
110 SEARCH%(2,1)=0:SEARCH%(2,2)=1
120 SEARCH%(3,1)=-1:SEARCH%(3,2)=0
130 SEARCH%(4,1)=0:SEARCH%(4,2)=-1
140 DIM TILES$(6)
150 TILES$(1)=CHR$(127)
160 TILES$(2)=" "
170 TILES$(3)="x"
180 TILES$(4)="X"
190 TILES$(5)="o"
200 TILES$(6)="O"
210 GOSUB 9000
220 P% = 1
230 VP% = 2


500 REM Main loop =====================
510 GOSUB 8000
520 PRINT "Pelaaja " P% ", anna koordinaatit";
530 INPUT X,Y
540 IF X<1 OR X>W% OR Y<1 OR Y>H% THEN 520
560 GOSUB 1000
570 IF OK% = 0 THEN 520
580 MAP%(X,Y) = TAKEN%(P%)
590 VP% = P%
600 P% = 2-P%+1
610 FOR Y = 1 TO H%
620 FOR X = 1 TO W%
630 GOSUB 1000
640 IF OK% = 1 THEN 510
650 NEXT X
660 NEXT Y
670 GOSUB 8000
680 PRINT "Pelaaja " VP% " voitti pelin!"
690 END


1000 REM Check coordinates ============
1010 OK% = 0:M%=MAP%(X,Y)
1020 IF M% = CLAIMED%(P%) THEN 1120
1030 IF M% = 0 OR M% = TAKEN%(P%) OR M% = TAKEN%(VP%) THEN RETURN
1060 FOR S=1 TO 4
1070 XX%=X+SEARCH%(S,1):YY%=Y+SEARCH%(S,2)
1080 IF XX%<1 OR XX%>W% OR YY%<1 OR YY%>H% THEN 1100
1090 IF MAP%(XX%,YY%) = TAKEN%(P%) THEN 1120
1100 NEXT S
1110 RETURN
1120 OK%=1
1130 RETURN


8000 REM Print map ====================
8010 FOR ROW = 1 TO H%
8020 FOR COL = 1 TO W%
8030 PRINT TILES$(MAP%(COL,ROW) + 1);
8040 NEXT COL
8050 PRINT 
8060 NEXT ROW
8070 RETURN


9000 REM Load map =====================
9010 FOR ROW = 1 TO H%: FOR COL = 1 TO W%
9020 READ MAP%(COL,ROW)
9030 NEXT COL: NEXT ROW
9040 RETURN

10000 REM Map data ====================
10010 DATA 0,0,2,0,1
10020 DATA 0,1,2,1,0
10030 DATA 0,1,0,1,0
10040 DATA 1,1,1,0,1
10050 DATA 0,0,1,4,4